apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: "io.freefair.android-software-components"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 9
        versionName '0.3.0'
    }
}

dependencies {
    implementation "androidx.annotation:annotation:${rootProject.ext.androidxVersion}"
    implementation "androidx.fragment:fragment:${rootProject.ext.androidxVersion}"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.2.22'
    testImplementation 'org.robolectric:robolectric:3.3'
}

task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

publishing {
    publications {
        android.libraryVariants.all { variant ->
            "maven${variant.name.capitalize()}Aar"(MavenPublication) {
                from project.components.findByName("${variant.name}AndroidLibrary")
                groupId = 'com.braintreepayments'
                version = "${android.defaultConfig.versionName}"
                description 'A library making it easy to open a url in a browser or Chrome Custom Tab and receive a response from the web page.'
                artifact(javadocsJar)
                artifact(sourcesJar)
            }
        }
    }
}
